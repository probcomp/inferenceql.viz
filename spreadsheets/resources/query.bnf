select = <'SELECT'> <ws> what (<ws> from)?
<what> = paren-selectable (<','> <ws> paren-selectable)*
<paren-selectable> = selectable | <'('> selectable <')'>
<selectable> = probability | star
<generate> = <'GENERATE *'> (<ws> gen-given)? <ws> <'FROM model'>
probability = <'PROBABILITY OF'> <ws> (prob-column | prob-binding) (<ws> prob-given)? <ws> <'FROM model'>
prob-column = column
prob-binding = binding
star = <'*'>
from = <'FROM'> <ws> <'('> generate <')'> <ws> limit
gen-given = <'GIVEN'> <ws> bindings
prob-given = <'GIVEN'> <ws> <star>
<bindings> = binding (<ws> <'AND'> <ws> binding)*
<binding> = column <'='> value
<column> = #'[a-zA-Z_]+'
value = #'\"([^\"]+)\"'
<limit> = <'LIMIT'> <ws> nat
nat = #'[0-9]+'
ws = #'\s+'
