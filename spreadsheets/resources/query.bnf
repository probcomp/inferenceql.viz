query               ::= <'SELECT'> <ws> selections (<ws> <'FROM'> <ws> source (<ws> <'WHERE'> <ws> conditions)?)?
<p-query>           ::= query | <'('> query <')'>
selections          ::= star | p-column-or-prob (<','> <ws>? p-column-or-prob)*
<p-column-or-prob>  ::= column-or-prob | <'('> column-or-prob <')'>
<column-or-prob>    ::= column-selection | probability-of
column-selection    ::= column-name
column-name         ::= symbol | <'"'> symbol <'"'>
conditions          ::= condition (<ws> <'AND'> <ws> condition)*
<condition>         ::= or-condition
                      | presence-condition
                      | absence-condition
                      | equality-condition
                      | predicate-condition
<subcondition>      ::= condition | and-condition
or-condition        ::= subcondition <ws> <'OR'> <ws> subcondition
and-condition       ::= condition (<ws> <'AND'> <ws> condition)+
presence-condition  ::= column-name <ws> <'IS NOT NULL'>
absence-condition   ::= column-name <ws> <'IS NULL'>
equality-condition  ::= column-name <ws>? <'='> <ws>? value
predicate-condition ::= column-name <ws>? predicate <ws>? value
predicate           ::= (#"<" | #">" | #"<=" | #">=")
source              ::= <'data'> | <'('> query <')'>

probability-of      ::= <'PROBABILITY OF'> <ws> event <ws> <'GIVEN'> <ws> constraints <ws> <'UNDER'> <ws> model-name
binding             ::= column-name <ws>? <'='> <ws>? value
constraints         ::= star | event (<','> <ws>? event)*
<event>             ::= binding | column-name
model-name          ::= symbol | <'"'> symbol <'"'>

star                ::= <'*'>

<value>             ::= float | int | string
float               ::= #'-?\d+\.\d+(E-?\d+)?'
int                 ::= #'-?\d+'
nat                 ::= #'\d+'
string              ::= #'\"([^\"]+)\"'
symbol              ::= #'[a-zA-Z]\w*'
ws                  ::= #'\s+'
