select = <'SELECT'> <ws> what (<ws> from)?
<what> = paren-selectable (<','> <ws> paren-selectable)*
<paren-selectable> = selectable | <'('> selectable <')'>
<selectable> = probability | star
<generate> = <'GENERATE *'> (<ws> gen-given)? <ws> using
probability = <'PROBABILITY OF'> <ws> (prob-column | prob-binding) (<ws> prob-given)? <ws> using
using = <'USING'> <ws> model
model = symbol
prob-column = column
prob-binding = binding
star = <'*'>
from = <'FROM'> <ws> <'('> generate <')'> <ws> limit
gen-given = <'GIVEN'> <ws> bindings
prob-given = <'GIVEN'> <ws> given-target
given-target = star | column
<bindings> = binding (<ws> <'AND'> <ws> binding)*
<binding> = column <'='> value
<column> = #'[a-zA-Z0-9_]+'
<value> = string | nat | float | int
<limit> = <'LIMIT'> <ws> nat
symbol = #'[a-zA-Z]\w*'
string = #'\"([^\"]+)\"'
nat = #'\d+'
float = #'-?\d+\.\d+(E-?\d+)?'
int = #'-?\d+'
ws = #'\s+'
